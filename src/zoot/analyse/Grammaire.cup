package zoot.analyse ;

import java.util.*;
import zoot.arbre.* ;
import zoot.arbre.expressions.* ;
import zoot.arbre.instructions.* ;
import zoot.exceptions.AnalyseSyntaxiqueException;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    VARIABLES, DEBUT, FIN, POINTVIRGULE, ECRIRE, EGAL;
terminal String    CSTENTIERE, CBOOL, IDF, ENTIER, BOOLEEN;

/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG, LINST;
non terminal Void LDEC;
non terminal Instruction INST;
non terminal Expression EXP;
non terminal String TYPE;
non terminal String[] DECL;

/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;

PROG        ::= VARIABLES LDEC DEBUT  LINST:li  FIN
                {:
                    RESULT = li ;
                :}
                ;

LINST       ::= LINST:li  INST:i
                {:
                    ((BlocDInstructions)li).ajouter(i) ;
                    RESULT = li ;
                :}
                |
                INST:i
                {:
                    BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                    b.ajouter(i) ;
                    RESULT = b ;
                :}
                ;

LDEC        ::= LDEC:ld DECL:d
                {:
                    Symbole s = new Symbole(d[1]);
                    Tds.getInstance().ajouter(d[0], s);
                :}
                |
                DECL:d
                {:
                    Symbole s = new Symbole(d[1]);
                    Tds.getInstance().ajouter(d[0], s);
                :}
                ;

INST        ::= ECRIRE EXP:e POINTVIRGULE
                {:
                    RESULT = new Ecrire(e, eleft + 1);
                :}
                |
                IDF:id EGAL EXP:e POINTVIRGULE
                {:
                    Idf idf = new Idf(id, idleft+1);
                    RESULT = new Affectation(idf, e, eleft+1);
                :}
                ;
    
EXP         ::= CSTENTIERE:c
                {:
                    RESULT = new ConstanteEntiere(c, cleft + 1) ;
                :}
                |
                IDF:i
                {:
                    RESULT = new Idf(i, ileft + 1) ;
                :}
                |
                CBOOL:c
                {:
                    RESULT = new ConstanteBooleene(c, cleft + 1) ;
                :}
                ;

TYPE        ::= ENTIER:e
                {:
                    RESULT = e;
                :}
                |
                BOOLEEN:b
                {:
                    RESULT = b;
                :}
                ;

DECL        ::= TYPE:t IDF:idf POINTVIRGULE
                {:
                    String[] array = new String[] {idf, t};
                    RESULT = array;
                :}
                ;
