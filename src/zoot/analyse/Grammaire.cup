package zoot.analyse ;

import java.util.*;
import zoot.arbre.* ;
import zoot.arbre.expressions.* ;
import zoot.arbre.instructions.* ;
import zoot.tds.* ;
import zoot.exceptions.*;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    VARIABLES, DEBUT, FIN, POINTVIRGULE, ECRIRE, EGAL, FONCTIONS, RETOURNE, PRNT_G, PRNT_D;
terminal String    CSTENTIERE, CBOOL, IDF, ENTIER, BOOLEEN;

/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG, LINST, LFONC;
non terminal Void LDEC;
non terminal Instruction INST;
non terminal Expression EXP;
non terminal String TYPE;
non terminal String[] DECL;
non terminal BlocDeFonction FONC;

non terminal Void RET;

/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;

PROG        ::= VARIABLES  LDEC  FONCTIONS  LFONC  DEBUT  LINST:li  FIN
                {:
                    RESULT = li ;
                :}
                ;

LFONC       ::= LFONC:lf  FONC:f
                {:
                    ((Programme)lf).addBlocDeFonction(f) ;
                    RESULT = lf ;
                :}
                |
                FONC:f
                {:
                    Programme b = new Programme(fleft + 1, fright+1) ;
                    b.addBlocDeFonction(f) ;
                    RESULT = b ;
                :}
                ;

FONC        ::= TYPE:t  IDF:id  PRNT_G PRNT_D  DEBUT  LINST:li  RET  FIN
                {:
                    EntreeFct e = new EntreeFct(id);
                    SymboleFct s = new SymboleFct(t);
                    Tds.getInstance().ajouter(e, s, tleft + 1, tright);
                    BlocDeFonction bl = new BlocDeFonction(tleft + 1, tright);
                    bl.programme = li.getProgramme() ;
                    RESULT = bl ;
                :}
                ;

RET         ::=  RETOURNE  EXP  POINTVIRGULE ;

LINST       ::= LINST:li  INST:i
                {:
                    ((Programme)li).ajouter(i) ;
                    RESULT = li ;
                :}
                |
                INST:i
                {:
                    Programme b = new Programme(ileft + 1, iright+1) ;
                    b.ajouter(i) ;
                    RESULT = b ;
                :}
                ;

LDEC        ::= LDEC:ld DECL:d
                {:
                    EntreeVar e = new EntreeVar(d[0]);
                    SymboleVar s = new SymboleVar(d[1]);
                    Tds.getInstance().ajouter(e, s, dleft + 1, dright);
                :}
                |
                DECL:d
                {:
                    EntreeVar e = new EntreeVar(d[0]);
                    SymboleVar s = new SymboleVar(d[1]);
                    Tds.getInstance().ajouter(e, s, dleft + 1, dright);
                :}
                ;

INST        ::= ECRIRE EXP:e POINTVIRGULE
                {:
                    RESULT = new Ecrire(e, eleft + 1, eright);
                :}
                |
                IDF:id EGAL EXP:exp POINTVIRGULE
                {:
                    EntreeVar e = new EntreeVar(id);
                    Idf idf = new Idf(e, idleft+1, idright+1);
                    RESULT = new Affectation(idf, exp, expleft+1, expright+1);
                :}
                ;
    
EXP         ::= CSTENTIERE:c
                {:
                    RESULT = new ConstanteEntiere(c, cleft + 1, cright+1) ;
                :}
                |
                IDF:i
                {:
                    EntreeVar e = new EntreeVar(i);
                    RESULT = new Idf(e, ileft + 1, iright+1) ;
                :}
                |
                CBOOL:c
                {:
                    RESULT = new ConstanteBooleene(c, cleft + 1, cright+1) ;
                :}
                ;

TYPE        ::= ENTIER:e
                {:
                    RESULT = e;
                :}
                |
                BOOLEEN:b
                {:
                    RESULT = b;
                :}
                ;

DECL        ::= TYPE:t IDF:idf POINTVIRGULE
                {:
                    String[] array = new String[] {idf, t};
                    RESULT = array;
                :}
                ;
